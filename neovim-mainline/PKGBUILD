# Maintainer: Gomasy <nyan@gomasy.jp>

pkgbase='neovim'
pkgname="$pkgbase-mainline"
pkgver=0.9.0
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('x86_64')
url='https://neovim.io'
backup=('etc/xdg/nvim/sysinit.vim')
license=('custom:neovim')
provides=("neovim=${pkgver}" 'vim-plugin-runtime')
conflicts=('neovim')
depends=('libtermkey' 'libuv' 'msgpack-c' 'unibilium' 'libvterm>0.1.4' 'luajit' 'libluv' 'tree-sitter')
makedepends=('cmake' 'ninja' 'lua51-mpack' 'lua51-lpeg' 'gperf' 'git' 'patchelf')
optdepends=('python-neovim: for Python 3 plugin support (see :help python)'
            'xclip: for clipboard support on X11 (or xsel) (see :help clipboard)'
            'xsel: for clipboard support on X11 (or xclip) (see :help clipboard)'
            'wl-clipboard: for clipboard support on wayland (see :help clipboard)')
source=('git+https://github.com/neovim/neovim.git')
sha256sums=('SKIP')

pkgver() {
    cd "$pkgbase"

    _major=$(cat CMakeLists.txt | awk '/NVIM_VERSION_MAJOR/{print substr($2, 0, index($2, ")") - 1)}')
    _minor=$(cat CMakeLists.txt | awk '/NVIM_VERSION_MINOR/{print substr($2, 0, index($2, ")") - 1)}')
    _patch=$(cat CMakeLists.txt | awk '/NVIM_VERSION_PATCH/{print substr($2, 0, index($2, ")") - 1)}')

    echo $_major.$_minor.$_patch.$(git describe --long | sed -r 's/^[^-]*-(.*)$/r\1/;s/^v//;s/-/./g')
}

build() {
    cd "$pkgbase"

    cmake \
        -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_BUNDLED=OFF \
        -W no-dev
    cmake --build build
}

check() {
    cd "$pkgbase/build"

    ./bin/nvim --version
    ./bin/nvim --headless -u NONE -i NONE -c ':q'
}

package() {
    cd "$pkgbase"

    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 runtime/nvim.desktop "$pkgdir/usr/share/applications/nvim.desktop"
    install -Dm644 runtime/nvim.appdata.xml "$pkgdir/usr/share/metainfo/nvim.appdata.xml"
    install -Dm644 runtime/nvim.png "$pkgdir/usr/share/pixmaps/nvim.png"

    # Make Arch vim packages work
    mkdir -p "$pkgdir/etc/xdg/nvim"
    echo "\" This line makes pacman-installed global Arch Linux vim packages work." > "$pkgdir/etc/xdg/nvim/sysinit.vim"
    echo "source /usr/share/nvim/archlinux.vim" >> "$pkgdir/etc/xdg/nvim/sysinit.vim"

    mkdir -p "$pkgdir/usr/share/vim"
    echo "set runtimepath+=/usr/share/vim/vimfiles" > "$pkgdir/usr/share/nvim/archlinux.vim"
}
