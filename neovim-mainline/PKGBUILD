# Maintainer: Gomasy <nyan@gomasy.jp>

pkgbase='neovim'
pkgname="$pkgbase-mainline"
pkgver=0.3.2
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('x86_64')
url='https://neovim.io'
license=('custom:neovim')
depends=('jemalloc' 'libtermkey' 'libuv' 'msgpack-c' 'unibilium' 'libvterm' 'luajit')
makedepends=('cmake' 'lua51-mpack' 'lua51-lpeg' 'gperf' 'git')
optdepends=('python2-neovim: for Python 2 plugin support (see :help python)'
            'python-neovim: for Python 3 plugin support (see :help python)'
            'xclip: for clipboard support (or xsel) (see :help clipboard)'
            'xsel: for clipboard support (or xclip) (see :help clipboard)')
conflicts=('neovim')
source=('git://github.com/neovim/neovim.git')
sha1sums=('SKIP')

pkgver() {
    cd "$pkgbase"
    echo $pkgver.$(git describe --long | sed -r 's/^[^-]*-(.*)$/r\1/;s/^v//;s/-/./g')
}

build() {
    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake "../$pkgbase" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_JEMALLOC=ON
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir" install

    install -Dm644 "../$pkgbase/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
