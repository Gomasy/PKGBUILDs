# Maintainer: Gomasy <nyan@gomasy.jp>

pkgbase='neovim'
pkgname="$pkgbase-mainline"
pkgver=0.7.0
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs'
arch=('x86_64')
url='https://neovim.io'
license=('custom:neovim')
depends=('libtermkey' 'libuv' 'msgpack-c' 'unibilium' 'libvterm' 'luajit' 'libluv' 'tree-sitter')
makedepends=('cmake' 'git' 'gperf' 'lua51-lpeg' 'lua51-mpack')
optdepends=('python-neovim: for Python 3 plugin support (see :help python)'
            'xclip: for clipboard support on X11 (or xsel) (see :help clipboard)'
            'xsel: for clipboard support on X11 (or xclip) (see :help clipboard)'
            'wl-clipboard: for clipboard support on wayland (see :help clipboard)')
provides=("neovim=${pkgver}" 'vim-plugin-runtime')
conflicts=('neovim')
source=('git://github.com/neovim/neovim.git')
sha256sums=('SKIP')

pkgver() {
    cd "$pkgbase"
    echo $pkgver.$(git describe --long | sed -r 's/^[^-]*-(.*)$/r\1/;s/^v//;s/-/./g')
}

build() {
    mkdir -p "$srcdir/build"
    cd "$srcdir/build"

    cmake "../$pkgbase" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir/build"
    make DESTDIR="$pkgdir" install

    install -Dm644 "../$pkgbase/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
