# Maintainer: Gomasy <nyan@gomasy.jp>

pkgname=nginx-mainline-boringssl
pkgver=1.17.6
pkgrel=1
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server, mainline release'
arch=('i686' 'x86_64')
url='https://nginx.org'
license=('custom')
depends=('pcre' 'zlib' 'geoip' 'mailcap')
makedepends=('cmake' 'git' 'go' 'rust')
backup=('etc/nginx/fastcgi.conf'
        'etc/nginx/fastcgi_params'
        'etc/nginx/koi-win'
        'etc/nginx/koi-utf'
        'etc/nginx/nginx.conf'
        'etc/nginx/scgi_params'
        'etc/nginx/uwsgi_params'
        'etc/nginx/win-utf'
        'etc/logrotate.d/nginx')
install=nginx.install
provides=('nginx' 'nginx-mainline')
conflicts=('nginx' 'nginx-mainline')
source=($url/download/nginx-$pkgver.tar.gz{,.asc}
        #"git+https://boringssl.googlesource.com/boringssl#branch=chromium-stable"
        "git+https://github.com/cloudflare/quiche.git"
        "git+https://github.com/openresty/headers-more-nginx-module.git"
        "git+https://github.com/google/ngx_brotli.git"
        "service"
        "logrotate"
        "nginx_dynamic_tls_records_and_http2_hpack.patch"
        "BoringSSL-enable-AES_CBC.patch"
        "BoringSSL-enable-OCSP.patch")
validpgpkeys=('B0F4253373F8F6F510D42178520A9993A1C052F8') # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('3cb4a5314dc0ab0a4e8a7b51ae17c027133417a45cc6c5a96e3dd80141c237b6'
            #'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
            'b9af19a75bbeb1434bba66dd1a11295057b387a2cbff4ddf46253133909c311e'
            '8e73bad4e42d1278a6e89f811b96bc1963389ae97ed855697382e31c15c3e2d7'
            'd4fa1203a9b8d08e0675bb455cc11e01f9ff72f676b173f83cd85c83aa36ed18'
            'eb9fdfc7701cbee297971cfbf6ee2bc602aa6ce56cdcc5cd29dfda2dcb203e88')

_common_flags=(
  --with-compat
  --with-debug
  --with-file-aio
  --with-http_addition_module
  --with-http_auth_request_module
  --with-http_dav_module
  --with-http_degradation_module
  --with-http_flv_module
  --with-http_geoip_module
  --with-http_gunzip_module
  --with-http_gzip_static_module
  --with-http_mp4_module
  --with-http_realip_module
  --with-http_secure_link_module
  --with-http_slice_module
  --with-http_ssl_module
  --with-http_stub_status_module
  --with-http_sub_module
  --with-http_v2_module
  --with-http_v2_hpack_enc
  --with-http_v3_module
  --with-mail
  --with-mail_ssl_module
  --with-pcre-jit
  --with-stream
  --with-stream_geoip_module
  --with-stream_realip_module
  --with-stream_ssl_module
  --with-stream_ssl_preread_module
  --with-threads
)

_mainline_flags=(
)

prepare() {
  cd ${srcdir}

  git -C quiche submodule update --init
  git -C ngx_brotli submodule update --init
  patch -p1 -d quiche/deps/boringssl < BoringSSL-enable-AES_CBC.patch
  patch -p1 -d nginx-$pkgver < BoringSSL-enable-OCSP.patch
  patch -p1 -d nginx-$pkgver < quiche/extras/nginx/nginx-1.16.patch
  patch -p1 -d nginx-$pkgver < nginx_dynamic_tls_records_and_http2_hpack.patch
}

build() {
  export CXXFLAGS="$CXXFLAGS -fPIC"

  cd ${srcdir}/quiche/deps/boringssl
  mkdir build && cd build && cmake ../ && make && cd ${srcdir}/quiche/deps/boringssl
  mkdir -p .openssl/lib && cd .openssl && ln -s ../include . && cd ../
  cp ${srcdir}/quiche/deps/boringssl/build/crypto/libcrypto.a ${srcdir}/quiche/deps/boringssl/build/ssl/libssl.a .openssl/lib

  cd ${srcdir}/nginx-$pkgver
  ./configure \
    --prefix=/etc/nginx \
    --conf-path=/etc/nginx/nginx.conf \
    --sbin-path=/usr/bin/nginx \
    --pid-path=/run/nginx.pid \
    --lock-path=/run/lock/nginx.lock \
    --user=http \
    --group=http \
    --http-log-path=/var/log/nginx/access.log \
    --error-log-path=stderr \
    --http-client-body-temp-path=/var/lib/nginx/client-body \
    --http-proxy-temp-path=/var/lib/nginx/proxy \
    --http-fastcgi-temp-path=/var/lib/nginx/fastcgi \
    --http-scgi-temp-path=/var/lib/nginx/scgi \
    --http-uwsgi-temp-path=/var/lib/nginx/uwsgi \
    --add-module=${srcdir}/headers-more-nginx-module \
    --add-module=${srcdir}/ngx_brotli \
    --with-openssl=${srcdir}/quiche/deps/boringssl \
    --with-quiche=${srcdir}/quiche \
    --with-cc-opt="$CFLAGS $CPPFLAGS" \
    --with-ld-opt="$LDFLAGS" \
    ${_common_flags[@]} \
    ${_mainline_flags[@]}

  touch ${srcdir}/quiche/deps/boringssl/.openssl/include/openssl/ssl.h
  make
}

package() {
  cd nginx-$pkgver
  make DESTDIR="$pkgdir" install

  sed -e 's|\<user\s\+\w\+;|user html;|g' \
    -e '44s|html|/usr/share/nginx/html|' \
    -e '54s|html|/usr/share/nginx/html|' \
    -i "$pkgdir"/etc/nginx/nginx.conf

  rm "$pkgdir"/etc/nginx/*.default
  rm "$pkgdir"/etc/nginx/mime.types  # in mailcap

  install -d "$pkgdir"/var/lib/nginx
  install -dm700 "$pkgdir"/var/lib/nginx/proxy

  chmod 755 "$pkgdir"/var/log/nginx
  chown root:root "$pkgdir"/var/log/nginx

  install -d "$pkgdir"/usr/share/nginx
  mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

  install -Dm644 ../logrotate "$pkgdir"/etc/logrotate.d/nginx
  install -Dm644 ../service "$pkgdir"/usr/lib/systemd/system/nginx.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$provides/LICENSE

  rmdir "$pkgdir"/run

  install -d "$pkgdir"/usr/share/man/man8/
  gzip -9c man/nginx.8 > "$pkgdir"/usr/share/man/man8/nginx.8.gz

  for i in ftdetect indent syntax; do
    install -Dm644 contrib/vim/${i}/nginx.vim \
      "${pkgdir}/usr/share/vim/vimfiles/${i}/nginx.vim"
  done
}

# vim:set ts=2 sw=2 et:
